#
# process HELP request and extra compilation defines for all source files
# variable USAGE_DESCRIPTION is expected to contain the useful information
if(DEFINED ENV{HELP})   # if environment variable HELP is defined, set variable HELP to its value
  set(HELP $ENV{HELP})
endif()
if(HELP OR help)      # recognize both upper and lower case forms (cmake ... -DHELP=value)
  unset(HELP CACHE)   # DO NOT CACHE
  unset(help CACHE)   # DO NOT CACHE
  message("USAGE:\n " ${USAGE_DESCRIPTION})
  macro(conditional_return)     # return command if HELP
      return()
  endmacro()
  return()
else()
  macro(conditional_return)     # NO-OP otherwise
  endmacro()
endif()
#
# get extra definitions for this configuration run
# these extra definitions should not be cached
if(EXTRA_DEFINES)
  string(REGEX REPLACE "[, ]" ";" LOCAL ${EXTRA_DEFINES})
  unset(EXTRA_DEFINES CACHE)
  foreach(ITEM IN ITEMS ${LOCAL})
    set(EXTRA_DEFINES "${EXTRA_DEFINES} -D${ITEM}")
    add_compile_options(-D${ITEM})
  endforeach()
endif()
if(EXTRA_DEFINES)
  message(STATUS "extra defines : ${EXTRA_DEFINES}")
endif()
#
# get extra compilation options for this configuration run
# these extra definitions should not be cached
if(EXTRA_OPTIONS)
  string(REGEX REPLACE "[, ]" ";" LOCAL ${EXTRA_OPTIONS})
  unset(EXTRA_OPTIONS CACHE)
  foreach(ITEM IN ITEMS ${LOCAL})
    set(EXTRA_OPTIONS "${EXTRA_OPTIONS} ${ITEM}")
    add_compile_options(${ITEM})
  endforeach()
endif()
if(EXTRA_OPTIONS)
  message(STATUS "extra compilation options : ${EXTRA_OPTIONS}")
endif()
